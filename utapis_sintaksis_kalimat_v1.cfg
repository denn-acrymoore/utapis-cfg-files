# Grammar CFG U-Tapis v1
# Note: Singkatan-singkatan di CFG ini:
# KAL               = Kalimat.
# FN                = Frasa Nomina.
# FV                = Frasa Verba.
# FP                = Frasa Preposisi.
# F_ADV             = Frasa Adverbia (adverbia bisa terdiri dari satu kata
#                     atau lebih).
# F_KJ_KOOR         = Frasa Konjungsi Koordinatif (hanya dua elemen sehingga
#                     tidak membutuhkan tanda koma).
# F_KJ_KOOR_KOMA    = Frasa Konjungsi Koordinatif (lebih dari dua elemen 
#                     sehingga membutuhkan tanda koma).
# V                 = Verba.
# P                 = Preposisi.
# N                 = Nomina.
# A                 = Artikel.
# ADJ               = Adjektiva.
# ADV               = Adverbia.
# NUM               = Numeralia.
# KJ_KOOR           = Konjungsi Koordinatif.
# KJ_SUB            = Konjungsi Subordinatif yang terdiri dari satu kata.
# KJ_ATR            = Konjungsi Antar Kalimat yang terdiri dari satu kata.
# KOMA              = Tanda Koma.
# TTK               = Tanda Tengah Kalimat : ; ... - -- 
# TAK               = Tanda Akhir kalimat . ? !
# KB                = Kurung Buka ( [ { <
# KT                = Kurung Tutup ) ] } >
# KTP_AWAL          = Kutip Awal sesuai aturan nltk (`` / double backtick).
# KTP_AKHIR         = Kutip Akhir sesuai aturan nltk ('' / dua kutip tunggal).


# ======================
# Aturan Kalimat Dasar 
# ======================
KAL -> FN FV TAK


# ==================================================
# Aturan Kalimat dengan Keterangan di Awal Kalimat 
# ==================================================
# E.g.: Dalam pengembangan bahasa, kita dapat memanfaatkan bahasa daerah.
#       (Dalam, nomina), (pengembangan, nomina), (bahasa, nomina), (,, td_koma),
#       (kita, nomina), (dapat, adverbia), (memanfaatkan, verba), 
#       (bahasa, nomina), (daerah, nomina), (., td_akhir_kal)
# E.g.: Atas perhatian Saudara, kami ucapkan terima kasih.
#       (Atas, preposisi), (perhatian, nomina), (Saudara, nomina), (,, td_koma),
#       (kami, nomina), (ucapkan, verba), (terima, verba), 
#       (kasih, nomina), (., td_akhir_kal)
KAL -> FP KOMA FN FV TAK
KAL -> FN KOMA FN FV TAK
KAL -> FV KOMA FN FV TAK
KAL -> FN FV KOMA FN FV TAK

# ============================================== 
# Aturan Kalimat dengan Konjungsi Koordinatif 
# ==============================================
# Konjungsi "dan", "atau", "serta".
# E.g.: Polisi menemukan pisau dan senjata api.
# E.g.: Polisi menemukan ganja, pisau, dan senjata api.
KAL -> F_KJ_KOOR FV TAK
KAL -> F_KJ_KOOR_KOMA FV TAK

# Konjungsi "tetapi", "melainkan", "padahal", "sedangkan", "lalu", "kemudian".
# E.g.: Polisi tiba di lokasi, tetapi pencuri tersebut sudah pergi.
KAL -> FN FV KOMA KJ_KOOR FN FV TAK


# ======================================
# Aturan Kalimat dengan tanda kutip
# ======================================
# NOTE: Semua konten di dalam tanda kutip dihapus karena
#       termasuk kalimat lisan yang terbebas dari aturan
#       gramatikal kalimat tertulis.
KAL -> KTP_AWAL TAK KTP_AKHIR
KAL -> KTP_AWAL KOMA KTP_AKHIR FV TAK
KAL -> KTP_AWAL KOMA KTP_AKHIR FN FV TAK
KAL -> FV KOMA KTP_AWAL TAK KTP_AKHIR
KAL -> FN FV KOMA KTP_AWAL TAK KTP_AKHIR


# ===============================================
# Aturan Kalimat dengan konjungsi subordinatif
# ===============================================
# Jika anak kalimat tidak mengandung nomina.
# E.g.: Ia bingung ketika disuruh menceritakan pengalamannya.
# (ketika, konj_subor), (disuruh, verba), (menceritakan, verba)
# (pengalamannya, nomina).
KAL -> FN FV KJ_SUB FV TAK
KAL -> KJ_SUB FV KOMA FN FV TAK

# Untuk konjungsi subordinatif yang terdiri dari dua kata.
# E.g.: "Oleh karena", "oleh sebab".
KAL -> FN FV KJ_SUB KJ_SUB FV TAK
KAL -> KJ_SUB KJ_SUB FV KOMA FN FV TAK

# Jika anak kalimat mengandung nomina.
# E.g.: Presiden menyatakan bahwa reshuffling akan dilakukan.
# (Presiden, nomina), (menyatakan, verba), (bahwa, konj_subor), 
# (reshuffling, nomina), (akan, adverbia), (dilakukan, verba).
KAL -> FN FV KJ_SUB FN FV TAK
KAL -> KJ_SUB FN FV KOMA FN FV TAK

# Untuk konjungsi subordinatif yang terdiri dari dua kata.
# E.g.: "Oleh karena", "oleh sebab".
KAL -> FN FV KJ_SUB KJ_SUB FN FV TAK
KAL -> KJ_SUB KJ_SUB FN FV KOMA FN FV TAK

# Untuk konjungsi subordinatif penegasan ('yakni', 'yaitu').
# E.g.: Titik kumpul sudah disepakati, yakni di lokasi yang telah ditentukan.
# NOTE: Kalimat inti ada di depan.
KAL -> FN FV KOMA KJ_SUB FV TAK
KAL -> FN FV KOMA KJ_SUB FN FV TAK
KAL -> FN FV KOMA KJ_SUB FP TAK


# ======================
# Aturan Frasa Nomina
# ======================
# Satu kata nomina.
FN -> N

# Nomina dengan artikel "Sang", "Yang", "Sri", "Si", dll.
FN -> A N

# Nomina dengan artikel "pun".
# E.g.: "Apa pun", "siapa pun", "berapa pun".
# NOTE: "pun" digabungkan bila merupakan konjungsi.
# NOTE: 12 kata pengecualian (untuk konjungsi) dengan artikel "pun" menurut 
# PUEBI = "adapun", "andaipun", "ataupun", "bagaimanapun", "biarpun", "kalaupun",
#         "kendatipun", "maupun", "meskipun", "sekalipun", "sungguhpun", 
#         "walaupun".
FN -> N A

# Nomina dengan artikel "per".
# E.g.: "per bulan", "per tahun", "per kapita".
FN -> A N

# Nomina dengan artikel "per" dalam pecahan.
# E.g.: dua per lima, 
FN -> NUM A NUM

# Nomina berupa numeralia.
FN -> NUM

# Nomina dengan adjektiva
# E.g.: Mantan personelnya (adjektiva, nomina).
# E.g.: Momen tepat (nomina, adjektiva).
FN -> N ADJ | ADJ N

# Nomina dengan simbol
# E.g.: 25%, @tribunnews ('@', 'tribunnews'), 
#       redaksi@tribunnews.com ('redaksi', '@', 'tribunnews.com')
FN -> NUM SIMBOL
FN -> SIMBOL NUM
FN -> N SIMBOL
FN -> SIMBOL N

# Nomina di dalam tanda kutip (kutipan yang tidak memiliki tanda baca di 
# dalamnya). 
# E.g.: "Avenger", "The Kingsman", dll.
FN -> KTP_AWAL KTP_AKHIR

# Nomina dengan preposisi.
FN -> FN FP


# ============================================================
# Aturan Frasa Nomina + Konjungsi Subordinatif Atributif
# ============================================================
# Konjungsi subordinatif atributif hanya berupa kata "yang".
# Nomina dengan konjungsi subordinatif atributif ("yang").
# E.g.: Pencuri itu mengenakan pakaian 'yang' bermotif kotak-kotak dan 
#       berwarna hijau.
# E.g.: Pencuri itu mengenakan pakaian 'yang' ia gunakan sebelumnya.
FN -> FN KJ_SUB FV
FN -> FN KJ_SUB FN FV


# ========================
# Rekursi Frasa Nomina
# ========================
FN -> FN FN


# =====================
# Aturan Frasa Verba
# =====================
# Frasa verba transitif (membutuhkan objek setelah kata kerja).
FV -> V FN | V V FN 

# Frasa verba intransitif (tidak membutuhkan objek setelah kata kerja).
FV -> V | V V

# Frasa verba intransitif + preposisi
FV -> FV FP 

# Frasa verba dimana predikat menggunakan nomina (bukan verba).
# E.g.: Buku itu cetakan Bandung. = (Buku itu, subjek), 
# (cetakan Bandung, predikat).
# E.g.: Bambang guru kelas 6 SD = (Bambang, subjek),
# (guru kelas 6 SD, predikat).
FV -> FN

# Frasa verba dengan nomina + adverbia (tanpa verba).
# E.g.: Internet bukan barang asing lagi.
# (Internet, nomina), (bukan, adverbia), (barang, nomina), 
# (asing, adjektiva), (lagi, adverbia).
FV -> F_ADV FN
FV -> FN F_ADV
FV -> F_ADV FN F_ADV

# Frasa verba dimana predikat menggunakan adjektiva (bukan verba).
# E.g.: Ia bingung. = (Ia, nomina), (bingung, adjektiva)
FV -> ADJ

# Frasa verba dengan adjektiva + adverbia (tanpa verba).
# E.g.: Ia amat sangat pusing
# (Ia, nomina), (amat, adverbia), (sangat, adverbia), (pusing, adjektiva)
# E.g.: Ia pusing sekali lagi
# (Ia, nomina), (pusing, adjektiva), (sekali, adverbia), (lagi, adverbia) 
FV -> F_ADV ADJ
FV -> F_ADV ADJ F_ADV
FV -> ADJ F_ADV

# Frasa verba dengan adjektiva + adverbia + verba.
# E.g.: ... tidak bisa melawan 50 orang sekaligus lagi.
# (tidak, adverbia), (bisa, verba), (melawan, verba), (50, numeralia),
# (orang, nomina), (sekaligus, adverbia), (lagi, adverbia).
FV -> F_ADV V | F_ADV V V
FV -> V F_ADV | V V F_ADV
FV -> F_ADV V F_ADV | F_ADV V V F_ADV

FV -> F_ADV V FN | F_ADV V V FN
FV -> V FN F_ADV | V V FN F_ADV
FV -> F_ADV V FN F_ADV | F_ADV V V FN F_ADV


# =============================================
# Aturan Frasa Verba + Konjungsi Koordinatif
# =============================================
# Frasa verba yang diikuti dengan konjungsi koordinatif.
FV -> FV F_KJ_KOOR
FV -> FV F_KJ_KOOR_KOMA


# ============================================================
# Aturan Frasa Verba + Konjungsi Subordinatif Perbandingan
# ============================================================
# Frasa verba dengan konjungsi subordinatif perbandingan (tanpa jeda di 
# antaranya).
# E.g.: .... lebih dari ...; ... sama dengan ...
# E.g.: Kecantikan Putri lebih dari kecantikan Putra.
FV -> KJ_SUB KJ_SUB FN

# Frasa verba dengan konjungsi subordinatif perbandingan (dengan jeda di 
# antaranya).
# E.g.: ...lebih ... dari ....; ... sama... dengan...
# E.g.: Bangunan ini akan dibuat 'lebih' tinggi dan 'lebih' besar 'daripada'
#       sebelumnya.
# E.g.: Suara Raisa 'sama' bagusnya 'dengan' suara Isyana. (bagusnya = nomina).
FV -> KJ_SUB ADJ KJ_SUB FN
FV -> KJ_SUB N KJ_SUB FN

# Melakukan pemisahan frasa agar bisa digabungkan dengan konjungsi koordinatif.
# # E.g.: Bangunan ini akan dibuat 'lebih tinggi' dan 'lebih besar' 'daripada
#         sebelumnya'.
FV -> KJ_SUB ADJ
FV -> KJ_SUB FN


# ==================================================================
# Aturan Frasa Verba + Konjungsi Subordinatif Tujuan + Alat + Cara
# ==================================================================
# Konjungsi Subordinatif Tujuan = "supaya", "agar", "untuk".
# Konjungsi Subordinatif Alat + Cara = "dengan", "tanpa".
# E.g.: Jonathan harus berlatih keras "agar" dirinya lolos dalam babak 
#       penyisihan bulu tangkis malam nanti.
FV -> FV KJ_SUB FN FV

# E.g.: Nenek harus minum obat "dengan" teratur "supaya" lekas sembuh.
# E.g.: Paman Joni pergi ke Lampung "dengan" mengendarai mobil Jeep miliknya.
# E.g.: Pesulap itu mampu memegang bara api "tanpa" menggunakan pelindung 
#       tangan. 
FV -> FV KJ_SUB FV


# ========================
# Aturan Frasa Preposisi
# ========================
FP -> P FN


# ========================
# Aturan Frasa Adverbia
# ========================
# Maksimal terdapat dua adverbia berturut-turut.
F_ADV -> ADV | ADV ADV


# =====================================
# Aturan Frasa Konjungsi Koordinatif
# =====================================
# Jika hanya ada dua nomina yang dihubungkan dengan konjungsi 
# koordinatif.
F_KJ_KOOR -> FN KJ_KOOR FN | FN KJ_KOOR FV | FV KJ_KOOR FN | FV KJ_KOOR FV

# Jika ada lebih dari dua nomina yang dihubungkan dengan konjungsi 
# koordinatif.
F_KJ_KOOR_KOMA -> FN KOMA F_KJ_KOOR_KOMA | KJ_KOOR FN
F_KJ_KOOR_KOMA -> FV KOMA F_KJ_KOOR_KOMA | KJ_KOOR FV


# =======================================================
# Aturan-Aturan Simbol Non-Terminal ke Simbol Terminal
# =======================================================
V               -> '<verba>'
P               -> '<preposisi>'
N               -> '<nomina>'
A               -> '<artikel>'
ADJ             -> '<adjektiva>'
ADV             -> '<adverbia>'
NUM             -> '<numeralia>'
KJ_KOOR         -> '<konj_koord>'
KJ_SUB          -> '<konj_subor>'
KJ_ATR          -> '<konj_atr>'
KOMA            -> '<td_koma>'
TTK             -> '<td_tengah_kal>'
TAK             -> '<td_akhir_kal>'
KB              -> '<kurung_buka>'
KT              -> '<kurung_tutup>'
KTP_AWAL        -> '<kutip_awal>'
KTP_AKHIR       -> '<kutip_akhir>'
SIMBOL          -> '<simbol>'